plugins {
    id "java-library"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.5"
}

group = "com.itsaky.androidide"
version = "1.0.0"

repositories {
    mavenLocal()
    mavenCentral()
}

task buildSharedObject {
    inputs.file "lib/langs.cc"
    inputs.file "lib/com_itsaky_androidide_treesitter_Languages.h"
    inputs.file "lib/ts.cc"
    inputs.file "lib/com_itsaky_androidide_treesitter_TreeSitter.h"
    outputs.file "libts.so"

    doLast {
        exec {
            commandLine 'git', 'submodule', 'update', '--init', '--recursive'
        }
        exec {
            commandLine "./build.py", "-o", "libts", "grammars/tree-sitter-python", "grammars/tree-sitter-java", "-v"
        }
    }
}

clean {
    delete "libts.so"
}

compileJava {
    options.compilerArgs += ["-h", "${project.projectDir.toString()}/lib"]
}

compileTestJava {
    dependsOn buildSharedObject
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter:5.6.2"
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }

    environment "JAVA_TREE_SITTER", "${project.projectDir.toString()}/libts.so"
}
