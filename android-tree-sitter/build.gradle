plugins {
    id 'com.android.library'
    id 'com.vanniktech.maven.publish' version '0.21.0'
}

android {
    namespace 'com.itsaky.androidide.treesitter'
    ndkVersion "24.0.8215888"
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33
        versionName project.findProperty("VERSION_NAME")
        versionCode Integer.valueOf(project.findProperty("VERSION_CODE"))
        externalNativeBuild {
            def proj = rootProject.projectDir.absolutePath
            cmake {
                arguments "-DPROJECT_DIR=${proj}",
                        "-DTS_DIR=${proj}/tree-sitter-lib"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

dependencies {
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'junit:junit:4.13.2'
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

clean.doLast {
    exec {
        workingDir tsDir
        commandLine "make", "clean"
    }
}